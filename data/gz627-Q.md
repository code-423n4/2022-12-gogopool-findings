
# Non-Critical and Low Issues

## Non Critical Issues

|      | Issue             | Instances |
| ---- |:----------------- |:---------:|
| NC-1 | Versioning issue  |     1     |
| NC-2 | Redundant code    |     1     |
| NC-3 | Code optimization | 1          |

### [NC-1] Versioning issue

Storage variable `version` is set to `1` in multiple contracts' constructors. So, a later setting of `version` will override any earlier settings. It might make sense to set the `version` value only in the main contract of the system, which saves code size of other contracts and makes `version` value more manageable.
In addition, there is no setting function to change the `version` value, which makes `version` meaningless. If `version` is not supposed to change, define it as a constant to save gas; otherwise define a setting function so that it can be changed at some stage. Or, give it as an option in `ProtocolDAO.upgradeExistingContract()` function which is a place to upgrade contracts, and seems a reasonable place to change `version` when necessary.


### [NC-2] Redundant code

[Link to code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/RewardsPool.sol#L77)
Remove below L77 to save gas as it is redundant.
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/RewardsPool.sol

//@audit L77 can be removed since the function uses named variable returns
77:   return (currentTotalSupply, newTotalSupply);
```


### [NC-3] Code optimization

Function `requireValidStaker` in `Staking` contract can be optimized in several aspects:
1.  Use named variable returns to save code size and deployment gas;
2.  Optimize/simplify logic to improve readability and save gas;
3.   Add Natspec @return to improve readability

[Link to original code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L385-L394)
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

385	/// @notice Verifying the staker exists in the protocol
  	/// @param stakerAddr The C-chain address of a GGP staker in the protocol
  	function requireValidStaker(address stakerAddr) public view returns (int256) {
  		int256 index = getIndexOf(stakerAddr);
  		if (index != -1) {
  			return index;
  		} else {
  			revert StakerNotFound();
  		}
394	}
```

Optimized code:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

385	/// @notice Verifying the staker exists in the protocol
  	/// @param stakerAddr The C-chain address of a GGP staker in the protocol
  	/// @return index of staker or revert if index equals `-1`
  	function requireValidStaker_optimized(address stakerAddr) public view returns (int256 index) { 
  		index = getIndexOf(stakerAddr);
  		if (index == -1) revert StakerNotFound();  //@audit simplify the logic
  	}
```

---
## Low Risk Issues


|     | Issue                                                                                                 | Instances |
| --- |:----------------------------------------------------------------------------------------------------- |:---------:|
| L-1 | `requireNextActiveMultisig()` always returns the 1st enabled Multisig                                 |     1     |
| L-2 | Missing check whether the receiving address exists may possibly stuck tokens                          |     1     |
| L-3 | `ClaimProtocolDAO.spend()` allows GGPTokens being sent to arbitrary address                           |     1     |
| L-4 | Missing checks whether a contract actually exists during contract registration exposes security risks |     1     |
| L-5 | Hardening security measures with modifier `onlySpecificRegisteredContract`                            |     2     |
| L-6 | Refactor `Staking.setRewardsStartTime()` function to reduce risks                                     |     1     |
| L-7 | Mitigation against GGP Rewards exploitation                                                           | 1          |

### [L-1] `requireNextActiveMultisig()` always returns the 1st enabled Multisig

Relevant code: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/MultisigManager.sol#L80-L91

As the name suggested, `MultisigManager.requireNextActiveMultisig()` should return the **next** enabled Multisig. However, it actually always returns the 1st registered and enabled Multisig. This is because it always searches from the beginning and returns the result as long as the criteria is matched.

**POC**:
The below Testcase created 4 multisigs and the first 2 were disabled. We can see from the test that `requireNextActiveMultisig()` always returns the 1st enabled multsig which is `rialto3`.

Test steps:
1. Copy the below Testcase into test file `MultisigManager.t.sol`;
2. Run the test.

```solidty
	function testRequireNextActiveMultisigAudit() public {
		
		vm.startPrank(guardian);
		multisigMgr.disableMultisig(address(rialto));
		address rialto1 = getActor("rialto1");
		multisigMgr.registerMultisig(rialto1);
		multisigMgr.enableMultisig(rialto1);

		address rialto2 = getActor("rialto2");
		multisigMgr.registerMultisig(rialto2);
		multisigMgr.enableMultisig(rialto2);

		// Disable the first two
		multisigMgr.disableMultisig(rialto1);
		multisigMgr.disableMultisig(rialto2);

		address rialto3 = getActor("rialto3");
		multisigMgr.registerMultisig(rialto3);
		multisigMgr.enableMultisig(rialto3);

		address rialto4 = getActor("rialto4");
		multisigMgr.registerMultisig(rialto4);
		multisigMgr.enableMultisig(rialto4);

		vm.stopPrank();

		assertEq(rialto3, multisigMgr.requireNextActiveMultisig());
		assertEq(rialto3, multisigMgr.requireNextActiveMultisig());
		assertEq(rialto3, multisigMgr.requireNextActiveMultisig());
	}
```

**Mitigation Steps**:
In order to find the **next** enabled Multisig, we have to know the **last one** used. So, we need an extra state variable, say **lastMultisigIndex**, to store the index of the Multisig selected last time.

Refactored function: Note - the below function is optimized in terms of gas saving.
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/MultisigManager.sol

  uint256 lastMultisigIndex;  //@audit This is the added state variable

	/// @notice Gets the next registered and enabled Multisig, revert if none found
	/// @dev There will never be more than 10 total multisigs. If we grow beyond that we will redesign this contract.
	function requireNextActiveMultisig() external view returns (address addr) {  //@audit Use named variable returns
		uint256 total = getUint(keccak256("multisig.count"));
		uint256 lastIndex = lastMultisigIndex; //@audit Caching state variable to save gas in the below for-loop
		bool enabled;
		uint256 index;
		for (uint256 i; i < total;) {  //@audit no need to initialize `i` for gas saving
  		       unchecked {  //@audit It's safe to apply `unchecked`
    		            index = (i + lastIndex) % total;
  		       }
		       (addr, enabled) = getMultisig(index);
		       if (enabled) {
  		           lastMultisigIndex = index;  //@audit Update state variable
		           return addr;
		       }
		       unchecked {   //@audit Apply `unchecked` to save gas as no overflow could happen
  			  ++i;  //@audit `++i` is more gas efficient than `i++`
		       }
		}
		revert NoEnabledMultisigFound();
	}
```

### [L-2] Missing check whether the receiving address exists may possibly stuck tokens

[Link to affected code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Vault.sol#L137-L160)
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Vault.sol

137	function withdrawToken(
			address withdrawalAddress,
			ERC20 tokenAddress,
			uint256 amount
		) external onlyRegisteredNetworkContract nonReentrant {
		// Valid Amount?
143		if (amount == 0) {
			revert InvalidAmount();
		}
		// Get contract key
		bytes32 contractKey = keccak256(abi.encodePacked(getContractName(msg.sender), tokenAddress));
		// Emit token withdrawn event
		emit TokenWithdrawn(contractKey, address(tokenAddress), amount);
		// Verify there are enough funds
151		if (tokenBalances[contractKey] < amount) {
			revert InsufficientContractBalance();
		}
		// Update balances
		tokenBalances[contractKey] = tokenBalances[contractKey] - amount;
		// Get the toke ERC20 instance
		ERC20 tokenContract = ERC20(tokenAddress);
158		// Withdraw to the withdrawal address, , safeTransfer will revert if it fails
159		tokenContract.safeTransfer(withdrawalAddress, amount);
160	}
```

**Impact**:

In the above function `withdrawToken()`, it does not check if the receiving address `withdrawalAddress` exists before using `solmate`'s `SafeTransferLib` (see L159). The comment on L158 indicates that the code expects `safeTransfer will revert if it fails`. However, the function will not revert when calling `safeTransfer()` as long as the `withdrawalAddress` is a valid address, regardless existent or not. Thus, the responsibility of ensuring the correctness of the `withdrawalAddress` is all on the caller. However, any error may occur in practice. So, tokens might be mistakenly sent to:
- address(0), resulting in token permenant loss;
- non-existing account, resulting in token stuck in the token contract.

Reference: [A similar finding of Medium Severity](https://github.com/code-423n4/2022-01-trader-joe-findings/issues/170).

**POC**:

Put the below test case in the test file: `Vault.t.sol`. The test case successfully withdrawed tokens to address(0) and non-existing accounts:
```solidity
	function testWithdrawTokenToNonexistingAccount() public {
		registerContract(store, "VaultTest", address(this));
		dealGGP(address(this), 10 ether);

		ggp.approve(address(vault), 10 ether);

		vault.depositToken("VaultTest", ggp, 1 ether);
		assertEq(vault.balanceOfToken("VaultTest", ggp), 1 ether);

		vm.expectRevert(Vault.InsufficientContractBalance.selector);
		vault.withdrawToken(address(this), ggp, 2 ether);

		//@audit transfer 1 ETH to zero address
		vault.withdrawToken(address(0x0), ggp, 1 ether);
		assertEq(ggp.balanceOf(address(0x0)), 1 ether);

		vault.depositToken("VaultTest", ggp, 2 ether);
		assertEq(vault.balanceOfToken("VaultTest", ggp), 2 ether);

		//@audit transfer another 1 ETH to zero address
		vault.withdrawToken(address(0x0), ggp, 1 ether);
		assertEq(ggp.balanceOf(address(0x0)), 2 ether); //@audit the balance at zero address is 2 ETH now.

		//@audit transfer 1 ETH to a non-existing account
		vault.withdrawToken(address(0x123456789), ggp, 1 ether);
		assertEq(ggp.balanceOf(address(0x123456789)), 1 ether);
	}
```

**Mitigation steps**:

1. Verify `withdrawalAddress` is not address(0);
2. It might be desirable to implement a `rescue` functionality (to rescue stuck tokens) in the token contract with some rescuing policy in place if possible. This will solidify users' confidence in using the system.


---

### [L-3] `ClaimProtocolDAO.spend()` allows GGPTokens being sent to arbitrary address

[Link to affected code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ClaimProtocolDAO.sol#L20-L35)
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ClaimProtocolDAO.sol

20	function spend(
		string memory invoiceID,
22		address recipientAddress,
		uint256 amount
		) external onlyGuardian {
			Vault vault = Vault(getContractAddress("Vault"));
			TokenGGP ggpToken = TokenGGP(getContractAddress("TokenGGP"));

			if (amount == 0 || amount > vault.balanceOfToken("ClaimProtocolDAO", ggpToken)) {
				revert InvalidAmount();
			}

			vault.withdrawToken(recipientAddress, ggpToken, amount);

			emit GGPTokensSentByDAOProtocol(invoiceID, address(this), recipientAddress, amount);
		}
```

**Impact**:

The `spend` function allows GGPTokens being sent to arbitrary address. It is desirable to verify the address before sending tokens to, otherwise tokens may be permenantly lost or trapped.

**POC**:

Put the below test case in the test file: `ClaimProtocolDAO.t.sol`. The test verifies that GGPTokens can be sent to zero address resulting in permenant lost.

```solidity
	function testSpendFundsToZeroAddress() public {
		//setup deposit
		vm.startPrank(guardian);
		ggp.approve(address(vault), 1000 ether);
		vault.depositToken("ClaimProtocolDAO", ggp, 1000 ether);

		//spend deposit to address(0)
		daoClaim.spend("Invoice1", address(0x0), 1000 ether);
		assertEq(ggp.balanceOf(address(0x0)), 1000 ether);
	}	
```

**Mitigation Steps**:
1. Verify `recipientAddress` is not address(0);
2. A better/sophisticated solution might be implemented in the token contract to prevent token being trapped. e.g. `rescue` functionality or `pull over push` pattern.

---
### [L-4] Missing checks whether a contract actually exists during contract registration exposes security risks

[Link to affected code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ProtocolDAO.sol#L190-L194)
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ProtocolDAO.sol

	/// @notice Register a new contract with Storage
	/// @param addr Contract address to register
	/// @param name Contract name to register
190	function registerContract(address addr, string memory name) public onlyGuardian {
		setBool(keccak256(abi.encodePacked("contract.exists", addr)), true);
		setAddress(keccak256(abi.encodePacked("contract.address", name)), addr);
		setString(keccak256(abi.encodePacked("contract.name", addr)), name);
194	}
```

**Impact and POC**:
When registering a contract using `registerContract(address addr, string memory name)`, the address `addr` is not verified whether it is an existing contract address. So, **any address** can be registered as a 'registered contract'. The worst case is that a malicious EOA account is registered as a 'registered contract' either mistakenly or maliciously. We know that a 'registered contract' has the power to set and delete storage variables. Thus, the registered EOA account can freely change or delete contract state variables stored in `Storage` contract. This is different from a real smart contract:
 - a smart contract can only call pre-coded functions and the code can be examined or audited;
 - an EOA account can freely make any function calls. There is no way to stop an EOA account from doing so.

**Mitigation Steps**:
In order to reduce risks, it is important to make sure `addr` is an address of an existing contract. This can (1) use Openzeppelin's Address Lib or (2) just check the code size at the `addr`.

---

### [L-5] Hardening security measures with modifier `onlySpecificRegisteredContract`

*Instances (2)*

Function calls are mostly guarded with `onlySpecificRegisteredContract` modifier except for those in contract `Storage` and `Vault` whose functions should be callable by all registered contracts. However, there are several functions in contract `Staking` that are not guarded with `onlySpecificRegisteredContract`. This is probably the oversight:

(1) `Staking.increaseAVAXAssignedHighWater()` on [Line 156](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L156) : this function is only called by contract `MinipoolManager`. It can use the modifier `onlySpecificRegisteredContract("MinipoolManager", msg.sender)` to harden the security:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

156   function increaseAVAXAssignedHighWater(address stakerAddr, uint256 amount) public onlySpecificRegisteredContract("MinipoolManager", msg.sender) {
```

(2) `Staking.resetAVAXAssignedHighWater()` on [Line 163](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L163) : this function is only called by contract `ClaimNodeOp`. It can use the modifier `onlySpecificRegisteredContract("ClaimNodeOp", msg.sender)` to harden the security:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

163   function resetAVAXAssignedHighWater(address stakerAddr) public onlySpecificRegisteredContract("ClaimNodeOp", msg.sender) {
```

A little more complicated issue about `Staking.setRewardsStartTime()` on [Line 204](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L204): This function requires refactoring and is addressed in the below [L-6].

---
### [L-6] Refactor `Staking.setRewardsStartTime()` function to reduce risks

[Link to code](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L201-L211)
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

201	        /// @notice Set the timestamp when the staker registered for GGP rewards
	        /// @param stakerAddr The C-chain address of a GGP staker in the protocol
203	        // TODO cant use onlySpecificRegisteredContract("ClaimNodeOp", msg.sender) since we also call from increaseMinipoolCount. Wat do?
		function setRewardsStartTime(address stakerAddr, uint256 time) public onlyRegisteredNetworkContract {
			int256 stakerIndex = requireValidStaker(stakerAddr);
			if (time > block.timestamp) {
				revert InvalidRewardsStartTime();
			}

			setUint(keccak256(abi.encodePacked("staker.item", stakerIndex, ".rewardsStartTime")), time);
		}
```

Function `Staking.setRewardsStartTime()`, which affects rewards calculation, is only called in Contract `ClaimNodeOp` and `MinipoolManager`. In order to reduce the risks of manipulation by other registered contracts, it is better to refactor the function to harden the security. Actually, there is a `TODO` task labelled on this function ([on Line 203](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L203)), indicating the requirement to solve the above mentioned issue.

*Refactoring steps:*

**(1) Changes to `Staking` contract**:

The above function can be refactored to three functions: one private/internal function, and two external functions as in the below:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol

201	        /// @notice Set the timestamp for GGP rewards
		/// @param stakerAddr The C-chain address of a GGP staker in the protocol
		/// @param time The timestamp
		function _setRewardsStartTime(address stakerAddr, uint256 time) private {
			int256 stakerIndex = requireValidStaker(stakerAddr);
			if (time > block.timestamp) {
				revert InvalidRewardsStartTime();
			}

			setUint(keccak256(abi.encodePacked("staker.item", stakerIndex, ".rewardsStartTime")), time);
		}

                /// @notice Set the timestamp when the staker registered for GGP rewards
		function setRewardsStartTime(address stakerAddr, uint256 time) external onlySpecificRegisteredContract("ClaimNodeOp", msg.sender) {
			_setRewardsStartTime(stakerAddr, time);
		}

                /// @notice Reset rewards startTime
		function resetRewardsStartTime(address stakerAddr) external onlySpecificRegisteredContract("MinipoolManager", msg.sender) {
			_setRewardsStartTime(stakerAddr, 0);
		}
```

**(2) Changes to `MinipoolManager` contract**: No changes required.

**(3) Changes to `ClaimNodeOp` contract**: Just need to change [Line 83](https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ClaimNodeOp.sol#L82-L84)
Original code:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ClaimNodeOp.sol

		if (minipoolCount == 0) {
83		    staking.setRewardsStartTime(stakerAddr, 0);
		}
```

Refactored code:
```solidity
File: https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/ClaimNodeOp.sol

		if (minipoolCount == 0) {
83		    staking.resetRewardsStartTime(stakerAddr);
		}
```

---

### [L-7] Mitigation against GGP Rewards exploitation

Relevant code:

https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L269-L279
https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L286-L304
https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/Staking.sol#L308-L315

This is to propose a mitigation strategy against the Known Issue "GGP Rewards Manipulation Before Distribution", by which a node operator can increase the GGP rewards up to 15 times, by initially staking 10% GGP bond and before the end of reward cycle staking another 140% GGP bond.

**Proposed Mitigation Steps**:

The issue is rooted at the use of the amount of GGP bond at the single point of time when the GGP Rewards calculation is performed. So, we can incorporate the time factor into the GGP rewards calculation, i.e. use time-averaged GGP bond in the GGP Rewards calculation to eliminate the exploitation.
The simplest formula is:
      timeAveragedGGPBond = sumOfProductOfGGPBondAndItsPeriod / totalPeriod
More complicated method can be used as well.
